plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

def varCompileSdk = 33
def varMinSdk = 24
def varTargetSdk = 33
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def varVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
def versionNameProd = "${versionMajor}.${versionMinor}.${versionPatch}"
def versionNameDev = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

android {
    namespace 'co.harismiftahulhudha.dansmultiprochallenge'
    compileSdk varCompileSdk

    defaultConfig {
        applicationId "co.harismiftahulhudha.dansmultiprochallenge"
        minSdk varMinSdk
        targetSdk varTargetSdk
        versionCode varVersionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SERVER_CLIENT_ID", "\"${localProperties["SERVER_CLIENT_ID"]}\""
        resValue "string", "facebook_app_id", "\"${localProperties["FACEBOOK_APP_ID"]}\""
        resValue "string", "fb_login_protocol_scheme", "\"${localProperties["FACEBOOK_LOGIN_PROTOCOL_SCHEME"]}\""
        resValue "string", "facebook_client_token", "\"${localProperties["FACEBOOK_CLIENT_TOKEN"]}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "environment"

    productFlavors {
        dev {
            dimension "environment"
            versionName versionNameDev
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "DMP Challenge - Dev"
            buildConfigField "String", "BUILD_VARIANT", "\"DEV\""
            buildConfigField "String", "BASE_URL", "\"${localProperties["BASE_URL_DEV"]}\""
        }

        prod {
            dimension "environment"
            versionName versionNameProd
            applicationIdSuffix ""
            resValue "string", "app_name", "DMP Challenge - Prod"
            buildConfigField "String", "BUILD_VARIANT", "\"PROD\""
            buildConfigField "String", "BASE_URL", "\"${localProperties["BASE_URL_PROD"]}\""
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // Lifecycle Components
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.1'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.45'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.45'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    // RX Java
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // OkHttp3
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    // Room
    implementation 'androidx.room:room-runtime:2.5.1'
    annotationProcessor 'androidx.room:room-compiler:2.5.1'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.15.1'

    // Circle Image View
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.3.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.facebook.android:facebook-android-sdk:latest.release'
}